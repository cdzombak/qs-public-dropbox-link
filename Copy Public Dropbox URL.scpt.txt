-- Put Dropbox folder path with trailing slash in the quotes, eg. "Dropbox/Public/"
property _publicFolderPOSIXPath : POSIX path of (path to home folder) & "Dropbox/Public/"

-- Put your web-accessible Dropbox folder URL with a trailing slash in the quotes, eg. "http://dropbox.dzombak.com/"
property _publicFolderURL : "http://dropbox.dzombak.com/"

-- adapted from http://qsapp.com/wiki/Dropbox_public_link_(AppleScript)
on findAndReplace(_toFind, _toReplace, _theText)
	set _astid to AppleScript's text item delimiters
	try
		set AppleScript's text item delimiters to _toFind
		set textItems to _theText's text items
		set AppleScript's text item delimiters to _toReplace
		tell textItems to set _editedText to beginning & _toReplace & rest
		set AppleScript's text item delimiters to _astid
		return _editedText
	on error a number b
		set AppleScript's text item delimiters to _astid
		error a number b
	end try
end findAndReplace

-- adapted from http://harvey.nu/applescript_url_encode_routine.html via http://brettterpstra.com/2011/02/04/launchbar-actions-for-url-encoding-and-decoding/
-- there's a corresponding urldecode function there, but we don't need it for this script.
on urlencode_except_forward_slash(theText)
	set theTextEnc to ""
	repeat with eachChar in characters of theText
		set useChar to eachChar
		set eachCharNum to ASCII number of eachChar
		if (eachCharNum ≠ 42) and (eachCharNum ≠ 47) and (eachCharNum ≠ 95) and (eachCharNum < 45 or eachCharNum > 46) and (eachCharNum < 48 or eachCharNum > 57) and (eachCharNum < 65 or eachCharNum > 90) and (eachCharNum < 97 or eachCharNum > 122) then
			set firstDig to round (eachCharNum / 16) rounding down
			set secondDig to eachCharNum mod 16
			if firstDig > 9 then
				set aNum to firstDig + 55
				set firstDig to ASCII character aNum
			end if
			if secondDig > 9 then
				set aNum to secondDig + 55
				set secondDig to ASCII character aNum
			end if
			set numHex to ("%" & (firstDig as string) & (secondDig as string)) as string
			set useChar to numHex
		end if
		set theTextEnc to theTextEnc & useChar as string
	end repeat
	return theTextEnc
end urlencode_except_forward_slash

-- adapted from http://qsapp.com/wiki/Dropbox_public_link_(AppleScript)
using terms from application "Quicksilver"
	on open _theseItems
		try
			repeat with _anItem in _theseItems
				tell application "Finder"
					set _path to POSIX path of (_anItem as text)
					-- TODO: warn if the given path isn't in the Dropbox Public folder
					set _fileName to my findAndReplace(_publicFolderPOSIXPath, "", _path)
					set _fileNameEncoded to my urlencode_except_forward_slash(_fileName)
					set _url to _publicFolderURL & _fileNameEncoded
					
					set the clipboard to _url
					-- Need a delay between copying to the clipboard
					if (count of _theseItems) > 1 then delay 1
				end tell
			end repeat
			
			-- These 2 lines (1 won't work) clear a multiple selection from Quicksilver's first pane. 
			tell application "Quicksilver" to set selection to missing value
			tell application "Quicksilver" to set selection to missing value
			
			tell application "Quicksilver" to set selection to _url
		on error a number b
			display dialog a
		end try
	end open
end using terms from
